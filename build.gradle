import com.github.jengelman.gradle.plugins.shadow.transformers.Log4j2PluginsCacheFileTransformer

plugins {
    id "java"
    id "application"
    id "io.freefair.lombok" version "8.13.1"
    id "com.gradleup.shadow" version "9.0.0-beta12"
}

group = "me.thosea"
version = "1.1.0"

repositories {
    mavenCentral()
}

dependencies {
    implementation("net.dv8tion:JDA:5.3.2") {
        exclude module: "opus-java" // audio library
    }
    implementation "com.zaxxer:HikariCP:6.3.0"
    runtimeOnly "org.xerial:sqlite-jdbc:3.49.1.0"

    implementation "org.apache.logging.log4j:log4j-api:2.24.3"
    implementation "org.apache.logging.log4j:log4j-core:2.24.3"

    runtimeOnly "org.apache.logging.log4j:log4j-slf4j2-impl:2.24.3"
}

application {
    mainClass = "me.thosea.autopoller.main.Bootstrap"
}

processResources {
    inputs.property "version", project.version

    filesMatching("autopoller_version.txt") {
        expand "version": inputs.properties.version
    }

    from "LICENSE"
    from "ThirdPartyLicenses"
    from "NOTICE"
}

java.toolchain {
    languageVersion = JavaLanguageVersion.of(22)
}

jar {
    archiveAppendix = "no_deps"
}

shadowJar {
    minimize {
        exclude(dependency("org.apache.logging.log4j:.*:.*"))
        exclude(dependency("org.xerial:sqlite-jdbc:.*"))
    }
    transform(Log4j2PluginsCacheFileTransformer)
    mergeServiceFiles()

//    include("**")
//    exclude("Log4j-**")

    archiveFileName = "autopoller-${version}.jar"
    doLast { tasks.jar.archiveFile.get().asFile.delete() }

    manifest {
        attributes "Multi-Release": "true"
    }
}

tasks {
    distZip.enabled = false
    distTar.enabled = false
    shadowDistZip.enabled = false
    shadowDistTar.enabled = false
}